apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: JacocoPlugin

compileKotlin.kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
compileTestKotlin.kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8

junitPlatform {
    platformVersion junitGradlePluginVersion
    filters {
        packages {
            include 'com.saantiaguilera.colors'
        }
        engines {
            include "junit-jupiter"
        }
    }
}

jacoco {
    toolVersion = "0.7.9"
}

configurations {
    testAgent {
        transitive = false
    }
}

tasks.withType(JavaExec) {
    if (it.name == 'junitPlatformTest') {
        doFirst {
            jvmArgs "-javaagent:${configurations.testAgent.singleFile}=destfile=${project.buildDir.name}/jacoco/test.exec"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
check.dependsOn jacocoTestReport

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"

    testAgent("org.jacoco:org.jacoco.agent:$jacocoAgentVersion:runtime")
}