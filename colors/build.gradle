apply plugin: 'kotlin'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: JacocoPlugin

compileKotlin.kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
compileTestKotlin.kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8

junitPlatform {
    platformVersion '1.0.2'
    filters {
        packages {
            include 'com.saantiaguilera.colors'
        }
        engines {
            include "junit-jupiter"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}
check.dependsOn jacocoTestReport

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    // configure jacoco to analyze the junitPlatformTest task
    jacoco {
        // this tool version is compatible with
        toolVersion = "0.7.6.201602180812"
        applyTo junitPlatformTestTask
    }

    // create junit platform jacoco task
    project.task(type: JacocoReport, "junitPlatformJacocoReport", {
        sourceDirectories = files("./src/main")
        classDirectories = files("$buildDir/classes/main")
        executionData junitPlatformTestTask
    })
    check.dependsOn "junitPlatformJacocoReport"
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.2"
    testCompile "org.junit.jupiter:junit-jupiter-engine:5.0.2"
    testCompile 'org.assertj:assertj-core:3.6.2'
}